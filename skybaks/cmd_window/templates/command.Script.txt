
#Include "TextLib" as TL

#Struct K_ServerData {
    Integer ID;
    Text Data;
    Integer Index;
    Integer Count;
}

declare Integer G_CommandDataId;

Void SendDataToServer(Text _Data, CMlEntry _Elem) {
    declare Integer MaxDataLen = 50;
    declare Text[] DataParts;
    declare Integer DataIndex = 0;
    while (DataIndex < TL::Length(_Data)) {
        declare Text NextDataPart = TL::SubString(_Data, DataIndex, MaxDataLen);
        DataIndex = DataIndex + TL::Length(NextDataPart);
        DataParts.add(NextDataPart);
    }

    foreach (Index => Part in DataParts) {
        declare ServerData = K_ServerData {
            ID = G_CommandDataId,
            Data = Part,
            Index = Index,
            Count = DataParts.count
        };

        _Elem.SetText(ServerData.tojson(), False);
        TriggerPageAction("{{ id }}__cmd_transmit_server_data");
    }
    _Elem.SetText("", False);

    G_CommandDataId = G_CommandDataId + 1;
}

main() {
    G_CommandDataId = 0;
    declare CMlTextEdit TextEdit_CmdEntry <=> (Page.GetFirstChild("textedit-cmd-entry") as CMlTextEdit);
    declare CMlEntry Entry_CmdDataDummy <=> (Page.GetFirstChild("cmd-data-dummy") as CMlEntry);
    declare Text PyApp_cmd_window_CommandWindowInputText for LocalUser;

    {% if not clear_input %}
        TextEdit_CmdEntry.Value = PyApp_cmd_window_CommandWindowInputText;
    {% endif %}

    while (True) {
        foreach (Event in PendingEvents) {
            if (Event.Type == CMlScriptEvent::Type::MouseClick && Event.ControlId == "button-execute-cmd") {
                SendDataToServer(TextEdit_CmdEntry.Value, Entry_CmdDataDummy);
            }
        }
        PyApp_cmd_window_CommandWindowInputText = TextEdit_CmdEntry.Value;
        yield;
    }
}
